---
title: X86汇编操作数寻址
categories:
  - [Windows]
  - [Asm]
date: 2019-09-18 20:28:16
updated: 2019-09-18 20:28:16
tags:
  - Windows
  - 逆向
  - Asm
---

还是在大学的时候学汇编原理和接口技术的时候看过这些东西，不甚明了，现在就来重新看一下。

<!--more-->

# 前言

粗略的来看，CPU 是一个计算单元，但其要计算的指令是从存储器，RAM 来的。然后一般来说，一个程序在运行的时候，会将硬盘中的代码以一定的形式解析后加载到 RAM 中，事实上，CPU 的工作，就是从 RAM 拿数据，拿指令，然后执行的过程。

因为数据和指令实际上都是在内存中的，只不过是分段而已。
另外，寄存器是固定大小的，只有 16/32/64 位，因此寄存器内只能存储一些数字而已，这些数字，根据我们如何使用它，他表达不同的意思。
如：立即数、一个地址。

所谓的寻址，就是把寄存器中的数字当成是一个地址，然后用 [AX] 的形式，让 CPU 去 AX 里面存储的值的内存单元处拿数据而已了。

CPU 是无法交换两个内存里面的内容的，必须通过寄存器

# 寄存器

X86 当前已经定义了很多的寄存器了。大体来说分为四类：

- 通用寄存器 EAX, EBX, ECX, EDX。
- 段寄存器 CS, DS, ES, GS, SS
- 索引和指针寄存器 ESI, EDI, EBP, EIP, ESP
- 标识寄存器 EFLAGS

## 通用寄存器

这个是用得最多的寄存器了，但是他们还可以分开来，当做 8/16 位的寄存器来使用：

```
32 bits : EAX EBX ECX EDX
16 bits : AX BX CX DX
 8 bits : AH AL BH BL CH CL DH DL
```

在这里 H，L 指的是寄存器的高字节和低字节：

```
EAX,AX,AH,AL : 加法寄存器（累加器）
               用于 I/O 端口访问，算数运算，中断调用等等。

EBX,BX,BH,BL : 基址寄存器。用来作为一个指向内存位置的指针。（在分段模式下，其指向的是 DS 寄存器内的）。
               还可用来获取一些中断的返回值。


ECX,CX,CH,CL : 计数寄存器。
               用以循环计数或者是位移。
               获取一些中断的返回值。

EDX,DX,DH,DL : 数据寄存器
               I/O端口访问，算数运算，一些中断调用
```

## 段寄存器

段寄存器保存的是在分段模式下各数据段，代码段的地址。他们是 16 位的值。，他们只能被通用寄存器设置值，或特殊的指令设置值。

```
CS         : 持有程序的代码段。
             改变它，可能会导致程序挂起。

DS         : 程序的数据段。
             改变它可能会导致获取不正确的数据。

ES,FS,GS   : 扩展寄存器，用来针对远距离指针寻址。

SS         : 程序使用的堆栈段寄存器。有些时候和 DS 一样。
             改变它的址会有不可预知的结果，其与很多数据相关。
```

## 索引和指针寄存器

索引和指针寄存器指定地址的偏移部分。用途很广，但是每个寄存器都有一个特殊的功能。有的时候用来和一个段寄存器配合，以指定一个很远的地址。 "E" 开头的寄存器只能在保护模式下使用。

```
ES:EDI EDI DI : 目的索引寄存器。
                用来做字符，内存数组的复制或者设置及配合 ES 进行远距离寻址。

DS:ESI EDI SI : 源索引寄存器。
                用来做字符，内存数组的复制或者设置

SS:EBP EBP BP : 堆栈基址寄存器。
                持有堆栈的基地址。

SS:ESP ESP SP : 堆栈寄存器。
                持有堆栈顶地址。

CS:EIP EIP IP : 指令寄存器。只读。
                下一条指令的偏移。
```

## 标志寄存器

```
Bit   Label    Desciption
---------------------------
0      CF      Carry flag
2      PF      Parity flag
4      AF      Auxiliary carry flag
6      ZF      Zero flag
7      SF      Sign flag
8      TF      Trap flag
9      IF      Interrupt enable flag
10     DF      Direction flag
11     OF      Overflow flag
12-13  IOPL    I/O Priviledge level
14     NT      Nested task flag
16     RF      Resume flag
17     VM      Virtual 8086 mode flag
18     AC      Alignment check flag (486+)
19     VIF     Virutal interrupt flag
20     VIP     Virtual interrupt pending flag
21     ID      ID flag
```

# 指令格式

指令在 **操作数** 上工作。

先是 **操作数类型** 和对应的 **寻址模式**：

- Register 寄存器：32 bit 或者 64 bit，表示存储在寄存器中的数据。一个存储在寄存器内的 offset 通常作为一个指向内存的指针使用。
- Immediate 立即数：指令中直接使用的常量。立即属不允许成为目的操作数。
- Direct Memory 直接内存：固定位置的内存
- Indirect Memory 间接内存：运行时通过存储在一个或者两个寄存器内的地址来决定内存的地址。

多操作数的指令总是 **从右至左** 工作。

```asm
mov dst, src
```

## OFFSET/LEA

**地址常量** 是一种特殊的 **立即数**，它由一个偏移量或段值组成。

`OFFSET` 操作符返回一个内存地址相对于当前段的偏移值，不同模块内的数据可能属于同一段，汇编器在一个段内不知道每个模块的真实偏移值。
因此，对于 `var` 的偏移值，即使是一个立即数，都是在链接阶段决定的。
指令 `lea eax, [LocalVar]` 与 `mov eax, OFFSET LocalVar` 等价

`LEA`（Load Effective Address-加载有效地址，也即是物理地址） 指令将内存偏移值加载到寄存器。但不会访问内存哦，只是借用了寻址的语法来表示要加载的地址。

关于 LEA 指令的展示：

```asm
lea eax, [ebx+8]
```

```
+------------------+                  +------------+
 | Registers        |                  | Memory     |
 +------------------+                  +------------+
 | EAX = 0x00000000 |       0x00403A40 | 0x7C81776F |
 | EBX = 0x00403A40 |       0x00403A44 | 0x7C911000 |
 +------------------+       0x00403A48 | 0x0012C140 |
                            0x00403A4C | 0x7FFDB000 |
                                       +------------+
```

指令执行完毕后，EAX 将会是 `0x00403A48`
作为对比：

```asm
mov eax, [ebax+8]
```

执行后，EAX 将会是 `0x0012C140`

**lea 并不会获取内存中的值，它仅仅是计算出一个内存地址，它决不会真正的访问内存地址，也不会更改标志寄存器。它是一个 shift-and-add 指令，但使用了内存操作数语法。**

# 内存和寻址模式

## 声明静态内存区域

我们可以通过特殊的汇编器指示来声明静态的数据区域（也叫做全局变量）。数据声明应该在前面放在 `.data` 指示符。在这个指示符后的，`.byte, .short, .long` 可以用来声明 1， 2 和 4 字节的数据位置。为了引用这些数据的地址，我们可以给他们打上标签。标签在汇编中是非常有用的，他们给内存地址赋予了后面会被汇编器和链接器找出来的名字。这和通过名字了声明变量相似，但有一些低级的规则。例如，按序声明的位置将会一个接一个的在内存中定位。如：

```asm
.DATA
var	DB 64  	; 单字节
var2	DB ?	; 单字节，未初始化
DB 10	; 单字节，无标签，位置是 var2 + 1
X	DW ?	; 2 字节，未初始化，
Y	DD 30000    	; 4字节，通过 Y 引用，初始化为 30000
```

高级语言的数组可以是多维的，可以通过索引访问，X86 汇编中的数组只是一些在内存中连续的地址。一个数组可能只是列出值来声明。对于字节数组的特殊情况，可以使用字符串字面值。如果要在很大区域进行填充为 0，可以使用 `.zero` 指示符。

```asm

Z	DD 1, 2, 3	; 3 个 4 字节值，初始化为 1， 2，3。Z + 8 处将是 3
bytes  	DB 10 DUP(?)	; 10 个未初始化的字节。
arr	DD 100 DUP(0)    	; 100 个 4 字节的 word，从 arr 开始，都初始化为 0
str	DB 'hello',0	; 6 字节的字面值常量。

```

## 寻址

现代的 X86 兼容的 CPU 可以寻址 $2^{32}$ 字节的内存：内存地址是 32 bit 宽。为了支持通过标签来访问内存区域（如常量），x86 提供了一个灵活的方式来计算和引用内存地址：最多可以 2 个 32bit 的寄存器和一个 32bit 的有符号常两可以相加在一起来计算内存地址。其中一个寄存器可选的可以预先乘以 2, 4 或者 8。

寻址模式可以很很多的 X86 指令一起使用（后面会说）。这里我们会通过 `mov` 指令在内存和寄存器移动数据来演示。这个指令有两个操作数：第一个目的操作数，第二个是源操作数（INTEL 格式）：

```asm
mov eax, [ebx] ## EBX 中的存储地址的 4 字节到 ex
mov [var], ebx ## EBX 中的内容到 var 地址处 的 4 字节中。
mov eax, [esi - 4] ## eax 到 ESI 中地址 - 4 字节处的四字节到 EAX 中
mov [esi + eax], cl ## cl 中的值到 ESI + EAX 地址
mov edx, [esi + 4*ebx] ## ESI + 4*EBX 地址处四字节到 EDX
```

## Size 指示符

通常，在给定内存地址的目标数据大小可以通过汇编代码来推断。例如，上面的代码中内存区域的大小可以通过寄存器操作数的大小来判断。当我们在加载一个 32bit 的寄存器时，汇编器会推断内存区域应该是 4 字节大小。当我们存储一个一字节的寄存器到内存时，汇编器会推断我们需要一个字节。

然而，在某些时候推断的地址可能是迷惑的。比如代码 `mov [ebx], 2`。这个指令应该将 2 到 EBX 处的 1 字节？或者是应该移动 2 的 32 字节表示到 EBX 内地址开始的 4 字节中？因为这都是可能的，汇编器必须显式的指令哪一个是正确的。Size 指示符 `BYTE PTR, WORD PTR, DWORD PTR`就是这个目的，表明多少字节需要。

如：

```asm

mov BYTE PTR [ebx], 2	; 将2移动到 EBX 中存储地址处。
mov WORD PTR [ebx], 2	; 将 2 的 16 bit 表示移动到 EBX 地址处的两字节中
mov DWORD PTR [ebx], 2    	; 2 的 32 bit 表示移动到 EBX 地址处的 4 字节中。

```

## 内存段与基础寄存器

在间接寻址中，**基本寄存器** 表示在哪个段内计算内存地址。所以我们需要知道哪个在间接寻址的时候是以哪个为寄存器为寄存进行寻址的。

- 如果基础寄存器是，EBP， ESP ，那么针对 SS 段进行寻址。然而，如果 EBP 有倍乘因子，那么就是在 DS 内寻址。
- 其他所有的基址寄存器都是在 DS 内。
- 使用两个寄存器的时候，只能有一个有倍乘因子。（最多只能有两个寄存器进行寻址操作）
- 有倍乘因子的那个寄存器，就是索引寄存器。
- 其他的寄存器就是基址寄存器。
- 如果没有倍乘因子，第一个就是基址寄存器。

```asm
        mov   eax, [edx][ebp*4] ; EDX base (not scaled - seg DS)
        mov   eax, [edx*1][ebp] ; EBP base (not scaled - seg SS)
        mov   eax, [edx][ebp]   ; EDX base (first - seg DS)
        mov   eax, [ebp][edx]   ; EBP base (first - seg SS)
        mov   eax, [ebp]        ; EBP base (only - seg SS)
        mov   eax, [ebp*2]      ; EBP*2 index (seg DS)
```

## 索引

是一个常量基址+寄存器。

- 固定地址（基址）+ 可变的寄存器偏移（操作数字段含有一个常量基址）
- 有效地址是将操作数字段加到寄存器上。
- 这也被叫做数组类型寻址，或者也叫做 偏移寻址。

```asm
            mov eax, [ ebx + 5 ]
            mov eax, [ ebx + esi + 5 ]
```

在 **[]** 内结合使用寄存器是有限制的：**[]** 不能同时出现 DI,SI，不能同时出现 EBX, EBP。

```asm
            add ax, Table[ bx ]
            add ax, [ Table + bx ]
            add ax, Table + [ bx ]
            add ax, [ bx ] + Table
```

这几种方式是相同的。

## 索引倍乘

基数+寄存器偏移\*倍乘因子

## 栈寻址

PUSH,POP。 ESP 会自动的增加或者减少。

## 跳转相对地址

EIP+offset

直接跳转到一个地址。

# 例子

我们用一个 c 代码来看一下。

```cpp
int add(int a, int b){
    return a+b;
}
int main(){
    int a = 2;
    int b = 3;
    return add(a,b);
}

```

编译成汇编代码：

```sh
c++  -S -mllvm --x86-asm-syntax=intel t.cpp
```

打开 t.s

```asm
	.section	__TEXT,__text,regular,pure_instructions
	.build_version macos, 10, 14	sdk_version 10, 14
	.intel_syntax noprefix
	.globl	__Z3addii               ## -- Begin function _Z3addii
	.p2align	4, 0x90
__Z3addii:                              ## @_Z3addii add 函数
	.cfi_startproc
## %bb.0:
	push	rbp	;基址寄存器入栈
	.cfi_def_cfa_offset 16
	.cfi_offset rbp, -16
	mov	rbp, rsp ; 基址寄存器指向栈顶
	.cfi_def_cfa_register rbp
	mov	dword ptr [rbp - 4], edi; 参数 a 的值复制到新的栈帧内存
	mov	dword ptr [rbp - 8], esi; 参数 b 的值复制到新的栈帧内存
	mov	esi, dword ptr [rbp - 4];  将栈帧内 a 的值给到 esi
	add	esi, dword ptr [rbp - 8];  加上参数 b 的值
	mov	eax, esi; 将结果给  eax
	pop	rbp; 恢复
	ret
	.cfi_endproc
                                        ## -- End function
	.globl	_main                   ## -- Begin function main
	.p2align	4, 0x90
_main:                                  ## @main 程序启动入口
	.cfi_startproc
## %bb.0:
	push	rbp	;基址寄存器入栈
	.cfi_def_cfa_offset 16
	.cfi_offset rbp, -16
	mov	rbp, rsp; 基址寄存器指向栈顶
	.cfi_def_cfa_register rbp
	sub	rsp, 16	; 堆栈寄存器下移 16 字节。
	mov	dword ptr [rbp - 4], 0 ; 栈顶位置第一个4字节设置为0
	mov	dword ptr [rbp - 8], 2 ; 栈顶位置第二个4字节设置为2 int a = 2;
	mov	dword ptr [rbp - 12], 3; 栈顶位置第三个4字节设置为3 int b = 3;
	mov	edi, dword ptr [rbp - 8]; 参数 a 传入 DI
	mov	esi, dword ptr [rbp - 12]; 参数 b 传入 SI
	call	__Z3addii ; 调用  add 函数。
	add	rsp, 16 ; 栈指到栈顶
	pop	rbp ; BP 值向原来的值。
	ret
	.cfi_endproc
                                        ## -- End function

.subsections_via_symbols

```

通过上面例子就能看到，事实上调用函数的时候传的参数，过程是这样的：

1. 将参数值复制到寄存器。
2. 为函数开新的栈帧
3. 寄存器中的值复制到栈上。
4. 通过寄存器计算结果。
5. 返回到主函数。

# 一个新的寻址方式（64） RIP 相对寻址

RIP 是程序计算器，通常是指下一条要执行的指令。我们通常会见到这样的指令：

```asm
mov rax,qword ptr [rip + 227]
lea	rdx, [rip + 237]
```

这样的代码。

实际上就是把 RIP 地址加上一个偏移值。

**对于位置无关的代码 `-fPIC`，可以使用 `lea` 来获取到到静态数据的指针**。

如：

```asm
lea	rdx, [rip + 237]
```

# cdecl 与 stdcall

cdecl 是 C 默认的调用约定：

- 参数从右至左入栈
- 调用者负责清理堆栈

上面的函数中生成的汇编代码中，main 函数如下：

```asm
_main:                                  ## @main
## %bb.0:
	push	ebp
	mov	ebp, esp
	sub	esp, 24 # 栈下移 24 字节
	mov	dword ptr [ebp - 4], 0 # 返回值
	mov	dword ptr [ebp - 8], 2 # 栈上 a 变量
	mov	dword ptr [ebp - 12], 3  # 栈上 b 变量
	mov	eax, dword ptr [ebp - 8] #  a 的值存在寄存器里面
	mov	ecx, dword ptr [ebp - 12] #  b 的值存在寄存器里面
	mov	dword ptr [esp], eax  #  a 传入栈顶
	mov	dword ptr [esp + 4], ecx #  b 传入栈顶上四字节
	call	_add
	add	esp, 24
	pop	ebp
	ret
```
